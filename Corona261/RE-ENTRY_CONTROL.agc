### FILE="Main.annotation"
## Copyright:	Public domain.
## Filename:	REENTRY_CONTROL.agc
## Purpose:	A section of Corona revision 261.
##		It is part of the source code for the Apollo Guidance Computer
##		(AGC) for AS-202. No original listings of this software are
##		available; instead, this file was created via disassembly of
##		the core rope modules actually flown on the mission.
## Assembler:	yaYUL
## Contact:	Ron Burkey <info@sandroid.org>.
## Website:	www.ibiblio.org/apollo/index.html
## Mod history:	2023-05-27 MAS  Created from Solarium 55.
## 		2023-06-19 MAS  Updated for Corona.


		SETLOC	56000

# 	THE PIPUP SECTION IS A TASK WHICH READS THE PIPAS EVERY .5
# SECONDS. ACCELERATION HISTORY OVER THE LAST TWO SECONDS (FOUR READINGS)
# IS MAINTAINED  IN THE XPIPBUF, YPIPBUF, AND ZPIPBUF REGISTERS. EACH PIPA
# READING IS SUMMED INTO THE XPIPSUM ETC. REGISTERS, AND EVERY FOURTH READ
# ING AVERAGE G IS CALLED TO USE THE SUMMED READINGS TO UPDATE POSITION
# AND VELOCITY AND SET THE PIPSUM REGISTERS TO ZERO.
#
#   GOES THRU PIPUP ONLY EVERY 2 SEC.  (ELSEWHERE MORE OFTEN.)

		CADR	REPIPASR
REPIPUP		CAF	-1
		TC	PIPUP +1	# REREAD PIPAS IF NECESSARY.
		
		CADR	PIPASR
PIPUP		CAF	-1
		TC	ISWCALL		#  GO TO PIPAREAD SUBROUTINE.
		
		CAF	THIRTN		# PICK UP AT REDO5.13
		TC	NEWPHASE
		OCT	5		# 5.13 RESTART.  (NO TIME CALL.)
		
REDO5.13	CAF	DT
		TC	WAITLIST
		CADR	PIPUP
		
		CS	PIPTIME +1
		TS	TBASE5		# SAVE NEW TIME.
		
		CAF	IPIPDT
		TC	WAITLIST
		CADR	INTERPIP	# READ ACCELERATION MORE OFTEN.
		
		CAF	PRIO16
		TC	FINDVAC
		CADR	ENTRYTOP
		
		CAF	TWO
		TS	PIPCTR
		
		CAF	FOURTN		# THIS WILL PICK UP AT ENTRYTOP + 3.
		TC	NEWPHASE
		OCT	5		# 5.14 RESTART.
		
		TC	TASKOVER



IPIPDT		DEC	50


#	JOB WHICH PROCESSES PIPA READINGS TO UPDATE POSITION AND VELOCI

ENTRYTOP	XCH	TENTRY		# UPDATE ENTRY TIME.
		AD	TWO
		XCH	TENTRY		# IN SECS SINCE START OF ENTRY.
		
		CAF	ONE
		TS	PIPAGE
		
		CS	DELVX
		TS	XPIPBUF +3
		CS	DELVY
		TS	YPIPBUF +3
		CS	DELVZ		# SAVE PIP COUNTS.
		TS	ZPIPBUF +3
		
		AD	TWO
		XCH	TENTRY
		
		TC	PHASCHNG
		OCT	01705		# 5.15 RESTART PICKS UP AT REFAZE4.
		TC	BANKCALL	# COMPENSATE THE PIPA DATA
		CADR	1/PIPA
		
REFAZE4		TC	INTPRET

		ITC	0		# GO TO AVERAGE G INTEGRATION ROUTINES TO
			CALCRVG		# USE PIPA READINGS TO UPDATE POSIITON AND
					# VELOCITY.
		TC	PHASCHNG
		OCT	02105		# 5.17 RESTART AT REFAZE8.
		
REFAZE8		TC	INTPRET


#  PROCESS AVERAGE G OUTPUT...SCALE IT AND GET INPUT DATA

SCALEPOP	VXSC	0
			VPIP		# KVSCALE = (12800/.3048)0(25766.1973 X2)
			KVSCALE		# KVSCALE = .81491944
		STORE	(V)		# V VECTOR
		
		TEST	2
		VXV	VXSC		# (VREL) = (V) + KWE UNITR*UNITW
		VAD
			RELVELSW	# SWITCH NUMBER.
			GETUNITV
			UNITR
			UNITW
			KWE
			(V)
		STORE	(V)		# NEW V VECTOR IS RELATIVE.

GETUNITV	NOLOD	1
		UNIT
		STORE	UNITV		# HALF MAX. OF COURSE.
		
		TSLT	0
			28D		# RESCALE MAGNITUDE SQUARED.
			2		# SHIFT COUNT.  (2 BECAUSE HALF-LEN SQRD
		STORE	VSQUARE		# VELOCITY SQUARED. (NORMAL SCALING)
		
		DSU	0		# LEQ = VSQUARE-1
			VSQUARE		# 4 G-S FULL SCALE
			FOURTH
		STORE	LEQ
		
		TSLT	0
			30D
			1
		STORE	V
		
		DOT	1
		TSLT
			(V)		# RDOT = V.UNITR
			UNITR
			1		# AND SCALE BACK UP, UNITS ARE 1/2.
		STORE	RDOT
		DMOVE	0		#  SAVE OLD DRAG VALUE.
			D
		STORE	DOLD
		
		ABVAL	2
		TSLT	DMP
		BZE
			DELV		# KASCALE=5.85 16384/(4 .3048 805 100)
			2		# 2 FOR 2 SEC,  3 FOR 1 SEC, 4 FOR  .5 --
			KASCALE		#  = 3.   /4                       /25
			SETMIND
		STORE	D		# ACCELERATION   (DRAG, ALMOST)
		
GETUNI		VXV	1		# UNI = UNIT(V*R)
		UNIT
			(V)		# COULD USE UNITV.
			UNITR
		STORE	UNI		# INERTIAL OR RELATIVE AS IS (V).

# 	SOME OF THE FOLLOWING TARGETTING MIGHT BE DONE LESS OFTEN THAN ABOVE.

		TEST	1		# THIS TEST MIGHT BE COMBINED WITH  THE
		DMOVE			# PREVIOUS ONE IF ALL CALCULATIONS ARE
			RELVELSW	# DONE EVERY CYCLE.
			GETETA
			PIPTIME
		STORE	DTEAROT


UPDATERT	ITC	0		# UPDATE PREDICTED TARGET VECTOR RT
			EARROT2
		
		NOLOD	1
		DOT	RTB		# SINCE (RT) UNIT VEC, THIS IS 1/4 MAX.
			UNI		# LATANG = RT.UNI
			FRESHPD
		STORE	LATANG		# LATANG = MAC LATANGLE/4 (NO 2 PI)
					# UNUSUAL SCALING FOR LATANG. ( = ASIN L.
		
GETANGLE	DOT	3		# THETA = ARCCOS(RT.UNITR)
		DSU	BPL		# RT IS UNIT VECTOR
		DAD	TSLT
		ACOS
			RT
			UNITR
			NEAR1/4		# TO IMPROVE ACCURACY, CALC RANGE BY
			TINYTHET	# TINYTHET IF HIGH ORDER PART OF
			NEAR1/4		# ARCCOS ARGUMENT IS ZERO
			1
		STORE	THETAH
		
		EXIT	0

REFAZE10	TC	PHASCHNG
		OCT	02205		# 5.18 RESTART AT REFAZE10
		TC	INTPRET

#	JUMP TO PARTICULAR RE-ENTRY PHASE.

SEQUENCE	ITCI	0		# BRANCHES TO VARIOUS CONTROL PHASES.
			GOTOADDR	# ADDRESS OF PHASE EQS STORED HERE.

#	GOTOADDR CONTAINS THE ADDRESS OF THE ROLL COMMAND EQUATIONS 
# APPROPRIATE TO THE CURRENT PHASE OF RE-ENTRY.  SEQUENCING IS AS FOLLOWS:
#
# INITROLL SET HERE INITIALLY. HOLDS INITIAL ROLL ATTITUDE UNTIL KAT  EX-
# 	   CEEDED. THEN HOLDS NEW ROLL ATTITUDE UNTIL VRTHRESH EXCEEDED.  
#	   THEN BRANCHES TO
#
# HUNTEST  THIS SECTION CHECKS TO SEE IF THE PREDICTED RANGE AT NOMINAL
#	   L/D FROM PRESENT CONDITIONS IS LESS THAN THE DESIRED RANGE
#	     IF NOT-- A ROLL COMMAND IS GENERATED BY THE CONSTANT DRAG 
#	     CONTROLLER.
#	     IF SO-- CONTROL AND GOTOADDR ARE SET TO  UPCONTRL
#	   USUALLY NO ITERATION IS INVOLVED EXCEPT IF THE RANGE DESIRED IS 
#	   TOO LONG ON THE FIRST PASS THRU HUNTEST.
#
# UPCONTRL CONTROLS ROLL DURING THE SUPER-CIRCULAR PHASE. UPCONTRL IS TERM
#	   INATED EITHER (A) WHEN THE DRAG (AS MEASURED BY THE PIPAS) 
#	   FALLS BELOW Q7 OR (B) IF RDOT IS NEGATIVE AND REFERENCE VL 
#	   EXCEEDS V.  IN CASE (A), GOTOADDR IS SET TO KEP2 AND IN (B) TO
#	   PREDICT3, SKIPPING THE KEPLER PHASE OF ENTRY.
#
# KEP2	   GOTOADDR IS SET HERE DURING THE KEPLER PHASE TO MONITOR DRAG. 
#	   SPACECRAFT IS INSTANTANEOUSLY TRIMMED IN PITCH AND YAW TO THE 
#	   COMPUTED RELATIVE VELOCITY VECTOR.  THE LAST COMPUTED ROLL 
#	   ANGLE IS MAINTAINED.  WHEN THE MEASURED DRAG EXCEEDS Q7+.5,
#	   GOTOADDR IS SET TO
#
# PREDICT3 THIS CONTROLS THE FINAL SUB-ORBITAL PHASE. ROLL COMMANDS CEASE
#	   WHEN V IS LESS THAN VQUIT.  AN EXIT IS MADE TO TERMALT WHEN
#	   TERMINAL ALTITUDE IS REACHED.


#	OUT OF SEQUENCE SUBSECTION TO COMPUTE ETA.

		BANK	30
GETETA		TEST	1
		DMP			# NOT USED AT LOW VEL WHERE THETA NEG.
			EGSW
			SUBETA		# BRANCHES IF INTO EQ. GLIDE PHASE.
			THETAH
			KTETA		#  = 1000X2PI/(2)E14 163.84
		STORE	ETA		# DONT REALLY WANT IT, BUT NO PUSH WANTED
		
GETETA2		NOLOD	1
		DAD
			PIPTIME
		STORE	DTEAROT
		
		ITC	0
			UPDATERT
		
SUBETA		DSU	1		# SWITCH FROM INERTIAL TO RELATIVE
		BPL	SWITCH		# VELOCITY WHEN V LESS THAN .5 VSAT.
			V
			FOURTH
			SUBETA2
			RELVELSW	# SHOULD CHANGE TO OFF (NON-BRANCH) STATE

SUBETA2		DMP	1
		DDV	ITC
			THETAH
			KT		# KT = RE(2 PI)/ 2 VS 16384 163.84
			V		#                        /2 VSAT
			GETETA2

SETMIND		DMOVE	0		# MAKE D NON-ZERO.
			1BITDP
		STORE	D
		
		ITC	0
			GETUNI

TINYTHET	NOLOD	3		# ENTER WITH  X-.249
		DSU	ABS		# GET 1/4 - MPAC
		TSLT	SQRT		# SCALE UP BEFORE SQRT.
		DMP			# HAS FACTOR FOR UP SCALING.
			1BITDP +1	# X = SQRT(2(1-COSX)),   IN RADIANS
			13D		# X/2PI=(1/64PI)(SQRT(((1-COSX)/4)2EXP13)
			KACOS
		STORE	THETAH
		
		RTB	0		# OFF TO PRE-SEQUENCER...
			REFAZE10


# 	MAINTAINS INITIAL ROLL UNTIL D = KAT, GOES INTO HUNTEST WHEN
# RDOT = VRCONT.



		BANK	27

HUNTCADR	CADR	HUNTEST

INITROLL	DSU	1		# IF D-.05 G NEG, GO TO LIMITL/D
		BMN	EXIT		#  IF NOT, SET .05 G SWITCH FOR SCS
			D
			.05G
			LIMITL/D
		
		TC	RELAYON		# SEND .05 G SIGNAL TO SCS.
		OCT	40100
		TC	NEWMODE		# SET MODE TO AFTER .05 G STATE.
		OCT	00064
		TC	INTPRET
		
		DSU	2		# IF KAT-D POS, GO TO OUT WITH COMMAND
		BPL	LODON		#     IF NEG,  L/D = LAD
		DMOVE
			KAT
			D
			LIMITL/D
			LAD		# WHEN D GREATER THAN KAT, L/D = LAD
		STORE	L/D
		
		DAD	1		# IF RDOT + VRCONT NEG, GO TO STEER
		BMN	EXIT		# IF POS, SET SELECTOR TO HUNTEST.
			RDOT
			VRCONT
			LIMITL/D	#  DO LATERAL CONTROL IF NEEDED.
					#				SPACER.
		CAF	HUNTCADR
		TS	GOTOADDR	# AND FALL THRU INTO HUNTEST.


#  ... HUNTEST SECTION... CHECKS TO SEE WHEN PREDICTED RANGE = DESIRED ONE
#
#					KEEP WITH ABOVE CODING........

PREHUNT		TC	INTPRET
		
HUNTEST		DMOVE	0
			V
		STORE	V1
		
		DMOVE	0
			D		# A0 = D
		STORE	A0

		BPL	1
		DDV	DAD
			RDOT
			HUNTESTA
			LAD/KC1
			V1
		STORE	V1

		DSQ	1
		DDV	DAD
			RDOT
			2HSL/KC2
			A0
		STORE	A0
		
HUNTESTA	TEST	1
		DMOVE	SWITCH
			HUNTIND
			HUNTEST1
			A0
			HUNTIND
		STORE	D0

		DSU	1
		BMN	DAD
			C19
			D0
			+3
			D0
		STORE	D0

		DDV	0
			-1/1600
			D0
		STORE	-1/D0
		
		DMOVE	0		# ZERO DIFFOLD THE FIRST TIME THRU.
			3ZEROS
		STORE	DIFFOLD
		
		DAD	0		# V1OLD = V1 + C18  (500FPS)
			V1		# MAKES OLD VCORR -500 AND NEW +500 ..
			C18		# INITIAL VALUE ON VCORR = -500 FPS SO
		STORE	V1OLD		# (IN+TIAL 0ALUES IF NEEDED ON 1ST PASS.
		
HUNTEST1	DMOVE	0		# Q7 = Q7F
			Q7F
		STORE	Q7

		DMP	2		# ALP = A0 2HSD/LWD / V1 V1
		DDV	RTB
		DDV
			A0
			2HS/LEWD
			V1
			FRESHPD
			V1
		STORE	ALP
		
		NOLOD	1
		BDSU	BDDV
			NEARONE		# FACT1 = V1 / (1 - ALP)
			V1
		STORE	FACT1
		
		DSU	1
		DMP	DDV		# FACT2 = ALP(ALP - 1) / AD
			ALP
			NEARONE
			ALP
			A0
		STORE	FACT2
		
		DMP	2		# VL = FACT1(1 - SQRT(ALP + Q7 FACT2))
		DAD	SQRT
		BDSU	DMP
			Q7
			FACT2
			ALP
			NEARONE
			FACT1
		STORE	VL
		
		NOLOD	2		# GAMMAL = LEWD(V1-VL)/VL
		BDSU	DMP
		DDV
			V1
			LEWD
			VL
		STORE	GAMMAL1		# GAMMAL1 USED IN UPCONTRL
		
		DSU	1		# IF VL-VMIN NEG, GO TO SHORT
		BMN
			VL
			VMIN
			PREFINAL	# PREFINAL = SHORT
		
		DSQ	0		# VBARS = VL VL
			VL
		STORE	VBARS
		
		DSU	1		# IF VSAT - VL NEG, GO TO CONSTD.
		BMN
			HALVE		# VSAT = .5
			VL
			BECONSTD	# GOTOADDR MAY BE SIDETRACKED.
		STORE	DVL		# DVL = VSAT - VL
		
		DMOVE	0
			HALVE		# VS = VSAT
		STORE	VS
		
		NOLOD	2
		DSU	BMN		# IF V1 GREATER THAN VSAT, GO ON.
		BDSU
			V1
			GETDHOOK
			DVL		# DVL = DVL - (VSAT-V1) = V1 - VL
		STORE	DVL
		
		DMOVE	0
			V1
		STORE	VS		# VS = V1
		
GETDHOOK	TSRT	3		# DHOOK = ((1-VS/FACT1)SQ -ALP)/FACT2
		DDV	BDSU
		TSLT	DSQ
		DSU	DDV
			VS
			1
			FACT1
			HALVE
			1
			ALP
			FACT2
		STORE	DHOOK
		
		TSRT	1		# RESCALE BY 32.
		DDV	DSU
			DHOOK
			5
			Q7
			CHOOK
		STORE	AHOOKDV
		
		NOLOD	4
		DAD	DMP		# GAMMAL = GAMMAL1-CH1 DVLSQ(1+AHOOK DVL
		DMP	DMP		#        /DHOOK VBARS
		DDV	DDV
		BDSU	BMN
			1/8TH
			CH1
			DVL
			DVL
			DHOOK
			VBARS
			GAMMAL1
			NEGAMA		# FIND CONDITIONS FOR GAMMAL = 0.
		STORE	GAMMAL

#   ... PREDICT RANGES FOR EACH PHASE OF TRAJECTORY...

RANGER		DSQ	1
		TSRT	BDSU		# COSG = 1-GAMMAL SQ/2 , TRUNCATED SERIES
			GAMMAL
			2
			HALVE
		STORE	COSG/2
		
		DSU	3
		DMP	DMP		# E=SQRT(1+VBARS-2)VBARS COSG COSG)
		DMP	TSLT
		DAD	SQRT
			VBARS
			HALVE
			VBARS
			COSG/2
			COSG/2
			2		# MULT BY 4
			C1/16		# E  (E/4 REALLY) INTO PD.
		
		DMP	2		# ASKEP/2 = ARCSIN(VBARS COSG SING/E)
		DMP	DDV
		ASIN	TSLT
			VBARS
			COSG/2
			GAMMAL
			-		# E FROM PD.
			1		# ASKEP INTO PD.
		
		DMP	1		# ASP1 = Q2 +Q3(VL-Q4) = Q2' + Q3 VL
		DAD	DAD
			VL
			Q3
			Q2		# ASP1 + ASKEP INTO PD.
		
		DSQ	3		# ASPUP = -C12 LOG(V1 V1 Q7/VBARS D)/GAMM
		DMP	DDV		#                                AL1
		DDV	RTB
		DMP	DDV
			V1
			Q7
			VBARS
			A0
			LOG
			C12
			GAMMAL1		# ASPUP UNTO PD
		
		DMP	1		# ASPDWN = KC3 RDOT V / A0
		DMP	DDV
			KC3
			RDOT
			V
			A0		# ASPDWN INTO PD.
		
		DSU	2		# ASP3 = Q5(Q6-GAMMAL)
		DMP	DAD
		DAD	DAD
			Q6
			GAMMAL		# ASP = ASP1+ASKEP+ASPUP+ASP3
			Q5		# ASP INTO PD
		
		NOLOD	1
		BDSU	TSRT
			THETAH
			4		# DIFF = (ASP-THETA)/16
		STORE	DIFF		# END OF TEST
		
		NOLOD	4
		ABS	DSU		# IF ABS((THETAH-ASP) - 25NM NEG,GOTOUPSY
		BMN	TEST		# IF HIND SET, GO TO GET LV THE REPEATWAY
		LODON	BMN		# IF DIFF NEG, GO TO CONSTD
		LODON	DSU		# VCORR = V1 - V1OLD
			25NM
			GOTOUPSY
			HIND
			GETVCOR
			DIFF
			DCONSTD		# SETTING UP DIFFOLD ON THE WAY
			
			V1
			V1OLD		# VCORR = V1 - V1OLD
		STORE	VCORR
		
GETVCOR		DSU	1
		DDV	BDDV
			DIFFOLD
			DIFF
			VCORR
			DIFF
		STORE	VCORR
		
		EXIT	0
		
		TC	PHASCHNG	# HAVE GROUP 3 PICK UP AT PREHUNT.
		OCT	02003		# 3.16 RESTART.
		
		CAF	ADENDEXT	# SIDETRACK NEXT PASS UNTIL THIS ONE DONE.
		TS	GOTOADDR	# ONLY AFTER RESTART IS LEFT AFTER DETOUR.
		
		TC	INTPRET
		
		NOLOD	2
		BDSU	BPL		# IS VCORR - 1000 POS.
		DAD			# VCORR = 1000
			VCORLIM
			CHKVL
			VCORR		# LEAVING VCORLIM IN MPAC
		STORE	VCORR
		
CHKVL		DAD	2
		DSU	BMN		# IF VL + VCORR - VSAT POS, VCORR=VCORR/
		LODON	TSRT
			VCORR
			VL
			HALVE
			GETNUV1
			VCORR
			1
		STORE	VCORR
		
GETNUV1		DAD	0
			V1
			VCORR
		STORE	V1
		
		DMOVE	0		# SAVE OLD VALUE OF ASP
			DIFF
		STORE	DIFFOLD
		
		TEST	1		# SET HIND AND GO TO HUNTEST1
		SWITCH	ITC
			HIND
			HUNTEST1
			HIND
			HUNTEST1



NEGAMA		NOLOD	1		# ENTER WITH GAMMAL IN  MPAC
		DMP	DMP		# FIND GAMMAL VL / 3
			VL
			1/3RD		#   .. AND PUSH DOWN PARTIAL RESULT..

		DAD	3
		DMP	DMP		#  DEL VL = GAMMAL VL/3)/(LEWD/3 - DVL(2/3
		DDV	DDV		#      + AHOOKDV)(CH1 GS/DHOOK VL))
		BDSU	BDDV
			AHOOKDV
			1/12TH
			DVL
			CH1
			DHOOK
			VL		# 2 OUT OF ,1 INTO PUSH
			LEWD/3
		
		DAD	0
			VL
		STORE	VL		#  VL = VL + DEL VL
		
		NOLOD	3		#  VL IS IN MPAC
		DDV	BDSU		#  Q7 = ((1-VL/FACT1)SQ - ALP)/ FACT2
		DSQ	DSU
		DDV
			FACT1
			NEARONE
			ALP
			FACT2
		STORE	Q7
		
		DSQ	0		# GET NEW VBARS.
			VL
		STORE	VBARS
		
		DMOVE	0
			3ZEROS
		STORE	GAMMAL		#  GAMMAL = 0
		
		ITC	0
			RANGER


UPCADR		CADR	UPCONTRL

GOTOUPSY	EXIT	0		# BACK TO BASIC.

HUNTDUMP	CAF	UPCADR		# RESET GOTOADDR
		TS	GOTOADDR
		TC	NEWMODE		# CHANGE MODE TO SIGNAL END OF HUNT-TEST.
		OCT	00065
		
		CS	ONE		# MAKE GROUP 3 INACTIVE WHEN DONE WITH
		TC	NEWPHASE	# 	THE ITERATIONS.
		OCT	3		# GROUP 3.
		
		TC	INTPRET		# ...AND FALL INTO UPCONTROL...


# THIS SECTION IS THE UPCONTROL FOR THE SUPERCIRCULAR PHASE

UPCONTRL	DSU	3		# IF V-V1 POS, GO TO DOWNCONTROL
		BPL	LODON		# IF D-Q7 NEG, GO TO KEP
		DSU	BMN
		LODON	BMN		# IF RDOT NEG, GO TO VLTEST
			V		# VLTEST TESTS FOR START OF FINAL PHASE
			V1
			DOWNCNTL
			D
			Q7
			KEP		# SET CONSTS. ETC FOR BALLISTIC PHASE.
			RDOT
			VLTEST

CONT1		DSU	1
		BPL
			D		# IF D-A0 NEG,L/D=LAD,GO TO 310
			A0
			GOPOSLAD
		DMP	2		# VREF=FACT1(1-SQRT(FACT2 D + ALP))
		DAD	SQRT
		BDSU	DMP
			D
			FACT2
			ALP
			NEARONE
			FACT1
		STORE	VREF
		
		NOLOD	1		# RDOTREF = LEWD(V1-VREF)
		BDSU	DMP
			V1
			LEWD
		STORE	RDOTREF
		
		DSU	1		# IF VSAT - VREF NEG, GO TO CONTINU2
		BMN	NOLOD		# NOLOD TO PUSH DOWN (HALVE-VREF)
			VS
			VREF		# NO RDHOOK UNTIL VREF LESS THAN VSAT.
			CONTINU2	# PUSHING DOWN IF NO BRANCH.
		
		NOLOD	5		# RDHOOK=CH1(AHOOKDVL/DVL(DV+1))DV DV
		DMP	DDV		#       /DHOOK VREF
		DAD	DMP		# WHERE DV = VS - VREF
		DMP	DMP
		DDV	DDV
		BDSU
			AHOOKDV
			DVL
			1/8TH
			CH1
			0		#  ABOVE SHOULD HAVE PUSHED INTO LOC 0.
			-
			DHOOK
			VREF
			RDOTREF
		STORE	RDOTREF		#  RDOTREF = RDOTREF - RDHOOK
		
CONTINU2	DSU	0
			A0		# FACTOR = (D-Q7)/(A0-Q7)
			Q7		#  PARTIAL RESULT IN PD
		
		DSU	1
		DDV
			D
			Q7
		STORE	FACTOR

		DSU	4		# L/D = LEWD
		DMP	DDV		#  -((RDOT-RDOTREF)F1/KB1+V-VREF)F1/KB2
		DAD	DSU
		DMP	DDV
		DAD	BOV
			RDOT
			RDOTREF
			FACTOR
			KB1
			V
			VREF
			FACTOR
			KB2		# DELTA L/D INTO PD
			LEWD
			GOMAXL/D
		STORE	L/D
		
NEGTEST		NOLOD	3		# IF L/D NEG, AND D-C20 POS, L/D = 0
		BPL	LODON
		DSU	BMN
		SWITCH	DMP
			LIMITL/D
			D
			C20
			LIMITL/D
			LATSW
			3ZEROS		# L/D=0 NO NEG LIFT
		STORE	L/D
		
		ITC	0
			LIMITL/D


#	CONSTANT DRAG CONTROLLER
DCONSTD		DMOVE	0
			DIFF		# SAVE OLD VALUE OF DIFF FOR NEXT PASS.
		STORE	DIFFOLD
		
		DMOVE	0		# V1OLD = V1
			V1
		STORE	V1OLD
		
BECONSTD	AXT,1	1		# RESETS GOTOADDR  TO GO TO HUNTEST
		SXA,1
			HUNTEST
			GOTOADDR

CONSTD		DMP	0
			LEQ
			-1/D0

		DSU	1
		DMP	DAD
			D
			D0
			C16

		DMP	3
		DDV	DAD
		DMP	DAD
		TSLT	BOV
			2HS		# RDOTREF = - 2 HS D0/V
			D0
			V
			RDOT
			C17
			-
			8D
			GOMAXL/D

		STORE	L/D
		
		ITC	0
			NEGTEST
		
DOWNCNTL	DSU	1		# RDTR = LAD(V1-V)
		DMP
			V1
			V
			LAD/KC1

		NOLOD	5
		DSQ	DDV
		BDSU	BDSU
		DMP	DSU
		DAD	DAD
		TSLT	BOV
			2HSL/KC2
			A0
			D
			C16
			RDOT
			LAD/256
			-
			8D
			GOMAXL/D
		STORE	L/D
		
		ITC	0
			LIMITL/D
		
KEP		EXIT	0
		
		CAF	KEPCADR		# SET GOTOADDR TO KEPLER PHASE.
		TS	GOTOADDR
		TC	NEWMODE		# SET MODE TO KEPLER PHASE.
		OCT	00066
		
		TC	INTPRET
		
KEPL		DAD	1		# IF Q7+KDMIN - D NEG, GO TO FINAL PHASE
		DSU	BMN
			Q7		#  MIN DRAG = Q7 + .5 FT/SEC/SEC
			KDMIN
			D
			PREFINAL	#  FALL THRU IF POS...

ENDEXIT		EXIT	0		#  GOTOADDR IS SET HERE DURING VLHUNT.
		TC	OVERNOUT
		
KEPCADR		CADR	KEPL
P3CADR		CADR	PREDICT3

VLTEST		DSU	1		# IF V-VL-C18 NEG,EGSW=1,SELECTOR=PREDIC
		DSU	BPL		# GO TO PREDICT3
			V
			VL
			C18
			CONT1

PREFINAL	SWITCH	1		# CHANGE GOTOADDR TO PREDICT3 AND FALL
		EXIT			# INTO PREDICT3
			EGSW
		
		CS	ONE
		TC	NEWPHASE
		OCT	3
		
		CAF	P3CADR
		TS	GOTOADDR	# SET TO PREDICT3 PHASE.
		TC	NEWMODE
		OCT	00067
		
		TC	INTPRET		# ... AND FALL INTO PREDICT3...


#   SUBORBITAL CONTROL  (REFERENCE TRAJECTOORY BY TABLE LOOK-UP.)

PREDICT3	DSU	4
		BMN	LODON		# IF V - VQUIT NEG,  STOP STEERING
		TEST
		VXV	DOT		# IF (RT)*UNITR.UNI NEG, SET GONEPAST
		BMN	EXIT
			V
			VQUIT
			STEER
			GONEPAST
			GONEGLAD
			RT
			UNITR
			UNI		# (MIGHT SAVE THIS FROM EARLIER..
			SETGPAST
		
		CS	NEG14
BACK		TS	JJ

		CS	V
		INDEX	JJ
		AD	VREFER		# VREF - V, HIGHEST VREF AT END OF TABLE.
		CCS	A		# IF VREF-V POS LOOP BACK
		CCS	JJ		# DECREMENT JJ, JJ CANNOT BE ZERO
		TC	BACK
		
		AD	ONE
		TS	TEM1B		# V-VREF IN TEM1B (MUST BE POSITIVE NUM)
		
		INDEX	JJ
		CS	VREFER
		INDEX	JJ
		AD	VREFER +1	# V(K+1) - V(K)     (POS NUM)
		XCH	TEM1B
		EXTEND
		DV	TEM1B
		TS	GRAD		# GRAD = (V-VREF)/(VK+1 - VK)   (POS NUM
		
		CAF	FIVE
BACK2		TS	M1
		CAF	DEC15
		AD	JJ
		TS	JJ
		INDEX	A
		CS	VREFER
		INDEX	JJ
		AD	VREFER +1	# X(K+1) - X(K)
		EXTEND
		MP	GRAD
		INDEX	JJ
		AD	VREFER
		INDEX	M1
		TS	FX		# FX = AK + GRAD (AK+1 - AK)
		CCS	M1
		TC	BACK2
		
		CAF	ZERO
		XCH	FX +1
		AD	D
		EXTEND
		MP	FX +5		# F1
		TS	TEM1B		# TEM1B= F1(D-DREF)
		
		CS	RDOT		# FORM RDOTREF - RDOT
		DOUBLE
		DOUBLE			# SCALE UP BY 8 FOR THIS PHASE.
		DOUBLE
		AD	FX +3		# RDOTREF
		EXTEND
		MP	FX +4		# F2
		AD	TEM1B		# ADD F2(DADV1-DADVR)
		AD	FX +2		# RTOGO
		XCH	PREDANG		# NO OVERFLOW SKIP PLEASE.
		TC	INTPRET
		
		SMOVE	4
		TSRT	BDSU		# THETAH - PRED ANGLE
		DDV	TSLT
		BOV	DAD
		BOV
			PREDANG
			3
			THETAH
			FX		# FX = DRANGE/D L/D = Y
			5
			GOMAXL/D
			LOD
			GOMAXL/D
		STORE	L/D
		
		ITC	0
			GLIMITER
		
GOPOSLAD	DMOVE	0
			LAD
		STORE	L/D
		ITC	0
			LIMITL/D

SETGPAST	SWITCH	0		# SHOULD BE BY TARGET IF HERE.
			GONEPAST

GONEGLAD	COMP	0
			LAD		# L/D = - LAD
		STORE	L/D
		
		ITC	0
			GLIMITER

GOMAXL/D	RTB	1
		DMP			# L/D = LAD SIG(MPAC)
			SIGNMPAC
			LAD
		STORE	L/D		# AND FALLS INTO LIMITL/D SECTION.

GLIMITER	DSU	2		# IF GMAX/2-D POS, GO TO LIMITL/D
		BPL	DAD		# IF GMAX-D NEG, GO TO GOPOSLAD
		BMN	DMP
			GMAX/2
			D
			LIMITL/D
			GMAX/2
			GOPOSLAD
			2HS		# 2HS(GMAX-D) INTO PD
		
		DMP	1
		DAD	DMP
			LEQ
			1/GMAX
			LAD		# 2HS(GMAX-D) (LEQ/GMAX+LAD) INTO PD
		
		DDV	3
		DAD	SQRT		# XLIM = SQRT(PD+(2HSGMAX/V)SQ)
		DAD	BPL		# IF RDOT+XLIM NEG, L/D=LAD
		ROUND
			2HSGMXSQ
			VSQUARE
			-
			RDOT		# KGLIM2= -5 SCALED
			LIMITL/D

		DMP	2
		DAD	DDV
		DDV	BOV
			KGLIM1
			LEQ
			-
			D
			KGLIM2
			GOPOSLAD
		STORE	L/D

# COMES HERE TO COMPUTE ROLL COMMAND, CHECK LATERAL ERRORS, AND STEER
LIMITL/D	TEST	1		# NO LATERAL CONTROL IF PAST TARGET
		DMP	DAD		# Y = KLAT VSQUARE + LATBIAS
			GONEPAST
			L355
			VSQUARE
			KLAT
			LATBIAS
		STORE	Y
		
L350		ABS	4		# IF ABS(L/D)-L/DCMINR NEG, GO TO L353
		DSU	BMN
		LODON	SIGN		# IF K2ROLL LATANG NEG, GO TO L357
		BMN	LODON
		TSRT			# Y = Y/2
			L/D
			L/DCMINR
			L353
			LATANG
			K2ROLL
			L357
			Y
			1
		STORE	Y

L353		SIGN	2		# IF LATANG SIGN(K2ROLL) - Y POS, SWITCH
		DSU	BMN		# IF POS, GO TO BL361 IN BASIC.
		TEST	RTB
			LATANG
			K2ROLL
			Y
			L355
			LATSW
			L355
			BL361

L355		DDV	2		# ROLLC = ACOS((L/D)/LAD)
		TSRT	ACOS
		SIGN			# ROLLC = ROLLC SIGN(K2ROLL)
			L/D
			LAD
			1
			K2ROLL
		STORE	ROLLC

STEER		TEST	1
		EXIT
			LATSW
			L380
STEER2		CS	ROLLC
		EXTEND
		MP	K3ROLL
		AD	K1ROLL
		TS	MPAC		#  AFRAID TO STORE POSS BAD NUM IN THETAD
		TC	+4		#  SKIPS ON OVERFLOW
		INDEX	A
		CAF	LIMITS		#  ALLOW OVERFLOW (GO TO NEGMAX FROM POSMX
		AD	MPAC
		
		INHINT
		TS	THETAD		#  THETAD = X CDU = ROLL
		CS	BIT3
		MASK	TMMARKER
		AD	BIT3		#  SET BIT 3 IN TMMARKER WHEN THETAD COMP
		TS	TMMARKER
OVERNOUT	TC	PHASCHNG	# RECYCLE GROUP 5 TO PIPUP WAIT STATE.
		OCT	01405		# 5.12 RESTART.  (PIPUP SYNCED WITH 2 SEC)
		
		TC	ENDOFJOB

BL361		CS	K2ROLL		# K2ROLL = - K2ROLL
		TS	K2ROLL
		
		CCS	L/D		# IF L/D POS, EXIT
		TC	RE-ENTER	# SO IT WONT STORE IN PUSH LIST.
		TC	RE-ENTER
		TC	+2
		TC	RE-ENTER
		
		CCS	K2ROLL		# K1ROLL = K1ROLL + K3ROLL SIGN(K2ROLL)
		CAF	K3ROLL		# K3ROLL = - .125
		TC	+2
		CS	K3ROLL
		AD	K1ROLL
		TS	K1ROLL
		TC	RE-ENTER	# SKIPS THIS ON OVERFLOW.
		
		INDEX	A		# POSMAX + 1 = NEGMAX  ETC.
		CAF	LIMITS		# NO LIMIT ON NUMBER OF ROLL REVS DURING
		AD	K1ROLL		#     ENTRY.   (ROUTINE IS GENERAL.)
		TS	K1ROLL		# STILL DOESNT WORRY ABOUT 2SCOMP, THO.
		TC	RE-ENTER



L357		SIGN	0
			L/DCMINR	# L/D = L/DCMINR SIGN(L/D)
			L/D
		STORE	L/D
		
		ITC	0
			L355

L380		SWITCH	1
		RTB
			LATSW
			STEER2


#	ROUTINE TO PREDICT AND SET PITCH ANGLE FOR 2ND ENTRY CONDITIONS.

UPTHETA3	CAF	PRIO14
		TC	FINDVAC
		CADR	UPTHETA1
		
		TC	TASKOVER
		
UPTHETA1	TC	INTPRET		#  STARTS OFF IN BASIC

UPTHETA		ITC	0		# FIND DESIRED SPACECRAFT ORIENTATION.
			GETUNB
		
		ITC	0		# GET OGC, IGC, MGC.,GIMBAL COMMANDS
			CALCGTA
		
		EXIT	0
		
		CS	IGC
		COM
		DOUBLE
		TS	THETAD +1	# COMMAND PITCH ANGLE. (NO SKIPS)
		
		CS	MGC
		COM
		DOUBLE			# CHANGE FROMINTERPRETER SCALING TO CDU
		TS	THETAD +2	# YAW, MG ANGLE COMMAND
		
		INHINT
		CAF	PITCHDT		# CALL UP ATTITUDE CONTROL LOOP PITCHDT
		TC	WAITLIST	# SECONDS AFTER FINISHING THIS TIME.
		CADR	UPTHETA3

UPNOVER		TC	ENDOFJOB





#   PIPAS ARE READ (BUT NOT CLEARED) AT A HIGHER RATE HERE.

INTERPIP	CS	PIPAX
		INDEX	PIPCTR
		TS	XPIPBUF
		CS	PIPAY
		INDEX	PIPCTR
		TS	YPIPBUF
		CS	PIPAZ
		INDEX	PIPCTR
		TS	ZPIPBUF
		
		CCS	PIPCTR
		TC	+4
		CAF	TWO
		TS	PIPCTR
		TC	PIPSETUP
		TS	PIPCTR
		
		CAF	IPIPDT
		TC	WAITLIST
		CADR	INTERPIP
		
		TC	TASKOVER



PIPSETUP	CAF	PRIO15		# LOWER THAN AVG G - HIGHER THAN UPTHETA.
		TC	NOVAC
		CADR	SMOOTHER
		TC	TASKOVER



SMOOTHER	TC	ENDOFJOB	# WHEN WRITTEN, FIND A GOOD BANK FOR THIS.

# DETERMINE TIME TO OPEN CHUTE HERE IF NEEDED

PITCHDT		DEC	200
DT		DEC	200		# MAIN LOOP DT.
DEC15		DEC	15

K3ROLL		DEC	-.125
ADENDEXT	CADR	ENDEXIT

# DEFINED BY EQUALS

ASP		EQUALS	0
Y		EQUALS	20D		# TEMP
GAMMAL1		EQUALS	22D
A1		EQUALS	FX


# 	TABLE USED FOR SUB-ORBITAL REFERENCE TRAJECTORY CONTROL.



VREFER		DEC	0		# REFERENCE VELOCITY SCALED V/51532.3946
		DEC	.006539		# 15 POINTS ARE STORED AS THE INDEPENDENT
		DEC	.020958		# VARIABLE AND THEN SIX 15 POINT FUNC-
		DEC	.040809		# TIONS OF V ARE STORED CONSECUTIVELY
		DEC	.076107
		DEC	.122156
		DEC	.165546
		DEC	.196012
		DEC	.271945
		DEC	.309533
		DEC	.356222
		DEC	.404192
		DEC	.448067
		DEC	.456023
		DEC	.67918		# HIGH VELOCITY FOR SAFETY
		
		DEC	-.008035	# DRANGE/DA	SCALED DRDA/(2700/805)
		DEC	-.008035
		DEC	-.010820
		DEC	-.016550
		DEC	-.026935
		DEC	-.042039
		DEC	-.058974
		DEC	-.070721
		DEC	-.098538
		DEC	-.107482
		DEC	-.147762
		DEC	-.193289
		DEC	-.602557
C17		DEC	-.99999
		DEC	-.99999
		
		DEC	0		# -DRANGE/DRDOT
		DEC	0		# SCALED((2VS/8 2700) DR/DRDOT)
		DEC	-.0494520 B-3
		DEC	-.0683663 B-3	
		DEC	-.1343468 B-3
		DEC	-.2759846 B-3
		DEC	-.4731437 B-3
		DEC	-.6472087 B-3
		DEC	-1.171693 B-3
		DEC	-1.466382 B-3
		DEC	-1.905171 B-3
		DEC	-2.547990 B-3
		DEC	-4.151220 B-3
		DEC	-5.813617 B-3
		DEC	-5.813617 B-3

		DEC	-.00642065 B3	# RDOTREF	SCALED (8 RDT/2VS)
		DEC	-.00642065 B3
		DEC	-.0134426  B3
		DEC	-.013947   B3
		DEC	-.013462   B3
		DEC	-.011813   B3
		DEC	-.0095631  B3
		DEC	-.00806946 B3
		DEC	-.006828   B3
		DEC	-.00806946 B3
		DEC	-.0109791  B3
		DEC	-.0151496  B3
		DEC	-.0179817  B3
		DEC	-.0159061  B3
		DEC	-.0159061  B3
		
		DEC	0		# RANGE TO GO SCALED RTOGO/2700
		DEC	0
		DEC	.00100		#	2.7 NM
		DEC	.0032963	#	8.9
		DEC	.0081852	#	22.1
		DEC	.017148
		DEC	.027926
		DEC	.037
		DEC	.063296
		DEC	.077889
		DEC	.098815
		DEC	.127519
		DEC	.186963
		DEC	.238148
		DEC	.238148
		
		DEC	-.042360	# -AREF   SCALED AREF/805
		DEC	-.042360
		DEC	-.052919
		DEC	-.074534
		DEC	-.101242
		DEC	-.116646
		DEC	-.122360
		DEC	-.127081
		DEC	-.147453
		DEC	-.155528
		DEC	-.149565
		DEC	-.118509
		DEC	-.034907
		DEC	-.007950
		DEC	-.007950

		DEC	.000371		# DRANGE/D L/D SCALED Y/2700
		DEC	.000371
		DEC	.004770
		DEC	.008081
		DEC	.016030
		DEC	.035815
		DEC	.069422
		DEC	.104519
		DEC	.122
		DEC	.172407
		DEC	.252852
		DEC	.363148
		DEC	.512963
		DEC	.558519
		DEC	.558519		# END OF STORED REFERENCE
		

#    CONTINUATION OF RE-ENTRY SECTION IN 2ND BANK.  (CONSTANTS AND M923=8

		BANK	30
NEARONE		2DEC	.999999999
1/3RD		2DEC	.333333333	# 	ONE THIRD
1/12TH		2DEC	.0833333333	# ONE TWELFTH
# VSAT = 25766.1973 FT/SEC.

# RE = 21,202,900  FEET

LEWD		2DEC	.2
LEWD/3		2DEC	.066666666	# .2/3
FOURTH		2DEC	.25
3ZEROS		2DEC	0
1BITDP		2OCT	0000000001	# DOUBLE PREC 1 BIT
		DEC	0
NEAR1/4		2OCT	0777700000	# 1/4 LESS 1 BIT IN UPPER PART.
C18		2DEC	.0097026346	# 500/2VS
KDMIN		2DEC	.00062111801	# .5/805
C1/16		2DEC	.0625		# 1/16
2HS/LEWD	2DEC	.0863933042	# (2 28500 25 32.2/(4 VS VS))/.2
Q2		2DEC	-.046388889	# -1002/21600 =(643/21600-Q2(23500/2VS))
Q3		2DEC	.167003132	# .07 2VS/21600
Q5		2DEC	.326388889	# .3 23500/21600
Q6		2DEC	.034893617	# 820/23500
Q7F		2DEC	.0074534161	# 6/805    (VALUE OF Q7 IN FIXED MEM.)
VMIN		2DEC	.34929485	# 18000/2VS
C12		2DEC	.00684572901	# 32 28500/(21102900 2 PI)
NEG.16		2DEC	-.16
KB1		2DEC	.29411765	# 1/3.4
KB2		2DEC	-.0057074322 	# -1/(.0034 2 VS)
C19		2DEC	.049689441	# 40/805
VCORLIM		2DEC	.019405269	# 1000/2VS
VQUIT		EQUALS	VCORLIM		# BOTH ARE 1000 FT/SEC
Q7F2		2DEC	0.0074534161
C20		2DEC	.21739130	# (175 FPSS) LIFT UP IF ABOVE C20
25NM		2DEC	.0011574074 B-4	# 25/(21600 16)		(25 NAUT MILES)
C16		2DEC	.314453125
KVSCALE		2DEC	.81491944	# 12800/(2 VS .3048)
KASCALE		2DEC	.97657358	# 5.85 16384/(4 .3048 100 805)
KTETA		2DEC*	.383495203 E2 B-14*	#  1000 2PI/16384(163.84)
KT		2DEC*	.157788327 E2 B-14*	# RE(2PI)/2VS(16384)163.84
.05G		2DEC	.002		# .05/25
LAD		2DEC	.3
LAD/256		2DEC	.001171875	# .3/256
KLAT		2DEC	.0075
LATBIAS		2DEC	.00003		# APPRX .5 NM/ 4(21600/2 PI)
KWE		2DEC	.120056652
KACOS		2DEC	.004973592	# 1/32(2PI)
LAD/KC1		2DEC	.375		# .3/.8
2HSL/KC2	2DEC	.00740514	# (2 28500 25 32.2/(4 VS VS) .3)/.7
CHOOK		2DEC	1 B-5		# .25/8
1/8TH		2DEC	.125
CH1		2DEC	.24		# 8 CH1/25		(CH1 = .75)
KC3		2DEC	-.082540747	# KC3 (2VS)SQ /2PI 2S 32.2 LAD R
LOD		2DEC	.18
VRCONT		2DEC	.0135836886	# 700/2 VSAT
HALVE		2DEC	.5
NEG1/8		EQUALS	K3ROLL		# ONLY A SINGLE PRECISION NUMBER.
L/DCMINR	2DEC	.2895		# LAD COS(15 DEG)
-1/1600		2DEC	-.000625
KGLIM1		2DEC	.012496993
KGLIM2		2DEC	-.07810621
GMAX/2		2DEC	.2		# 5/25
1/GMAX		2DEC	.4
2HS		2DEC	.0172786611	# 2 28500 25 32.2/(4 VS VS)
2HSGMXSQ	2DEC	.000047768341	# ((2 28500 322/(4VS VS))SQ
KWIEM		2DEC	.147323336	# RESULT IN METERS OVER 12800
COS(13)		2DEC	.9763		# REAL COS OF 12.5 DEG. NOT 1/2 COSINE.
-SIN(13)	2DEC	-.21644		# REAL SIN OF 12.5 DEG.  NO FACTOR OF 1/2

# ... END OF RE-ENTRY CONSTANTS ...


#	CLOSED SUBROUTINE TO COMPUTE DESIRED NAV BASE ORIENTATION
#	NEEDED DURING ENTRY PHASE.
#
#	ENTER WITH VN, UNITR, UNITW, AND ROLLC
#	COMPUTES UXNB, UYNB, AND UZNB.  (NAV BASE UNIT VECTORS)



GETUNB		RTB	3		# RESET PUSH COUNTER.
		VXV	VXSC		# GET VELOCITY OF ATMOSPHERE
		VAD	UNIT		# GET UVA., RELATIVE VELOCITY
		VXSC			# RESOLVE BY COS OF 13 DEG.
			FRESHPD
			UNITR
			UNITW		# NEG OF REAL VEL.
			KWIEM		# EARTH RATE CONST IN METERS X 4.
			VN
			COS(13)		# UVA COS(13) INTO PD.
		
		NOLOD	1
		VXV	UNIT		# UYA = UNIT(VA*RN)
			UNITR
		STORE	16D		# UYA INTO LOC 16 OF VAC AREA.
		
		COS	1		# COS(ROLLC)
		VXSC			# UYA COS(ROLLC).  PUSH INTO LOC 6.
			ROLLC
			16D
		
		SIN	0
			ROLLC		# SIN(ROLLC) INTO PD AT LOC 12.
		
		VXV	2		# UNA = UNIT(UYA*UVA)
		UNIT	VXSC		# UNB = UYA COS(ROLL) + UNA SIN(ROLL)
		VAD	UNIT		# UNIT COULD BE REPLACED BY  VSLT   1.
			16D		#   UYA STORED AT LOC 16
			0		# UVA FIRST ITEM INTO PUSH LIST
		STORE	UYNB		# 1 SCAL AND  1 VEC FROM PD.
		
		NOLOD	2
		VXV	UNIT		# UN = UNIT (UYNB*UVA)
		VXSC	VSU		# UXNB = -UN SIN(13) -UVA COS(13)
			0
			-SIN(13)	# PULLS UVA COS(13) FRON PD.
		STORE	UXNB
		
		NOLOD	1
		VXV	VSLT		# UZNB = UXNB*UYNB
			UYNB
			1
		STORE	UZNB
		
		ITCQ	0		# AND RETURN.



ROLLCTEM	EQUALS	14D


#    SUBROUTINE TO READ PIPA COUNTERS, TRYING TO BE VERY CAREFUL SO THAT
# IT WILL BE RESTARTABLE.
#
#	(ARRIVE IN INTERRUPTED STATE OR INHIBITED AFTER RESTART.)
#    (EXIT IS THRU  ISWRETRN)

PIPASR		CS	ZERO		# PUT THESE INTO THE IMPOSSIBLE STATE
		TS	TEMX		# FOR THEIR INITIAL VALUES.
		TS	TEMY
		TS	TEMZ
		CAF	ZERO
		TS	DELVX +1
		TS	DELVY +1	# PIP COUNTERS MAY NOT HAVE POS ZERO IN
		TS	DELVZ +1
		TS	PIPAGE		# ZERO THIS TO INDICATE IN PIPA READING.

REPIP1		TC	READTIME +1	# PROBABLY NOT NEEDED SINCE NOT MUCH
		CS	RUPTSTOR +1	# CHANCE OF TIME1 OVERFLOWING NOW.  (BUT
		TS	PIPTIME +1	# JUST POSSIBLE IF MANY RESTARTS.
		CS	RUPTSTOR
		TS	PIPTIME
		
		CS	PIPAX
		TS	TEMXY
		XCH	TEMX		# PUT NEGZERO INTO PIPACTRS AS READ.
		XCH	PIPAX
REPIP1B		TS	DELVX
		TS	DELVX +1	# DOUBLE SAVE.

REPIP2		CS	PIPAY
		TS	TEMXY
		XCH	TEMY
		XCH	PIPAY
REPIP2B		TS	DELVY
		TS	DELVY +1

REPIP3		CS	PIPAZ		# REPEAT PROCESS FOR Z PIPA.
		TS	TEMXY		# SAVE NEG OF PIPA READ
		XCH	TEMZ		# SAVE HERE AS PICK UP NEGZERO
		XCH	PIPAZ		# RESETTING PIPA AS READ OUT)
REPIP3B		TS	DELVZ		# AND STORE IN Z.
		TS	DELVZ +1	# SHOWS THAT IT REALLY MADE IT.

REPIP4		CS	ZERO		# LEAVE THESE AT NEGZERO
		TS	DELVX +1
		TS	DELVY +1
		TS	DELVZ +1
		CS	BIT1
		MASK	TMMARKER
		AD	BIT1		# SET BIT 1 IN TM MARKER.
		TS	TMMARKER
		TC	ISWRETRN	# AND EXIT.  SHOULD HAVE COME THRU ICALL


#	ROUTINE TO RESTART IF READING PIPA COUNTERS.

REPIPASR	CCS	PIPAGE		# WAS I READING PIPS.
		TC	PIPASR		# NO.. PIPAGE = PLUS CONST.
		CCS	DELVZ +1	#  PIPAGE = 0  (I WAS READING PIPS.)
		TC	REPIP4		#  Z WAS READ OK.
		TC	+3		#  Z NOT DONE, CHECK Y.
		TC	REPIP4
		TC	REPIP4
		CCS	DELVY +1	# HAS IT CHANGED FROM ITS +ZERO INIT VALU
		TC	+3		# YES, Y DONE. TRY TO REDO Z.
		TC	CHKDELVX	# NO, GO LOOK AT X.
		TC	+1		# YES.
		CCS	TEMZ		# DOES TEMZ STILL = -0.
		TC	+4		# NO TRY TO RESTORE
		TC	+3
		TC	+2
		TC	REPIP3		# YES, GO BACK AND READ Z AGAIN.
		CS	TEMXY		# MUCH MORE LOGIC COULD BE INCORPORATED
		TC	REPIP3B		# TO CHECK PIPA CTR FOR SIZE.

CHKDELVX	CCS	DELVX +1	# HAS THIS CHANGED.
		TC	+3		# YES
		TC	CHKTEMX		# NO.
		TC	+1		# YES
		CCS	TEMY
		TC	+4
		TC	+3
		TC	+2
		TC	REPIP2
		CS	TEMXY
		TC	REPIP2B

CHKTEMX		CCS	TEMX		# HAS THIS CHANGED.
		TC	+4		# YES
		TC	+3		# YES
		TC	+2		# YES
		TC	REPIP1		# NO
		CS	TEMXY
		TC	REPIP1B


#  ENTRY INITIALIZATION ROUTINE.
#  -----------------------------
STARTENT	TC	PHASCHNG	# KEEP UPTHETA RUNNING IN CASE OF RESTART.
		OCT	03204		# 4.26 RESTART.
		
		TC	PHASCHNG	# PICK UP CURRENT STRING AT NEWMODE63.
		OCT	02305		# 5.19 RESTART.  RESYNCING PIPUP ALSO.
		
		INHINT
		CAF	PRIO14		# ESTABLISH UPTHETA FOR 1ST TIME.
		TC	FINDVAC
		CADR	UPTHETA1	# START UPTHETA JOB.
		
		RELINT
NUMODE63	TC	NEWMODE
		OCT	00063		# ENTRY PHASE = MODE 63
		
		CAF	INITCADR
		TS	GOTOADDR
		CAF	EARGCADR
		TS	CALCG
		CAF	ZERO
		TS	TENTRY
		CAF	NUDELTAT
		TS	DELTAT
		CAF	NU/PIPDT
		TS	1/PIPADT
		TC	INTPRET
		
		RTB	2		# SET CDUX AGAIN JUST IN CASE.
		VXV	DOT
		COMP	RTB
			CDUXFIX
			VN
			UNITR
			RTINIT		# THIS GIVES LATANG.
			SIGNMPAC	# GETS SIGN OF MPAC
		STORE	K2ROLL		# K2ROLL = -SIGN(LATANG)
		
		DOT	1
		TSLT	ACOS
			UNITR
			RTINIT
			1
		STORE	THETAH
		
		VMOVE	0
			RT
		STORE	RTINIT
		
		COMP	0
			DTEAROT
		STORE	DTEAROT
		
		ITC	0
			EARROT1
		RTB	0
			OVERNOUT	# EXIT SETTING RESTART BITS TO 5.12 FOR EN



INITCADR	CADR	INITROLL
EARGCADR	CADR	CALCGEAR
NUDELTAT	DEC	200 B5
NU/PIPDT	DEC	200 B6
DOWNVEL		2DEC	18.4375 B-6
KAT		2DEC	.008
