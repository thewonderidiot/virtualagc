### FILE="Main.annotation"
## Copyright:	Public domain.
## Filename:	ORBITAL_INTEGRATION_FOR_202.agc
## Purpose:	A section of Corona revision 261.
##		It is part of the source code for the Apollo Guidance Computer
##		(AGC) for AS-202. No original listings of this software are
##		available; instead, this file was created via disassembly of
##		the core rope modules actually flown on the mission.
## Assembler:	yaYUL
## Contact:	Ron Burkey <info@sandroid.org>.
## Website:	www.ibiblio.org/apollo/index.html
## Mod history:	2023-05-27 MAS  Created from Solarium 55.
## 		2023-06-17 MAS  Updated for Corona.


		SETLOC	60000

AVETOMD1	AXC,1	2
		ITA	SXA,1
		ITC
			1
			MIDEXIT
			MEASMODE
			AVETOMID

AVETOMD2	AXC,1	1
		ITA	SXA,1
			2
			MIDEXIT
			MEASMODE

AVETOMID	VMOVE	6
		AXT,1	SXA,1
		AXT,1	SXA,1
		AXT,1	SXA,1
		AXT,1	SXA,1
		AXT,1	SXA,1
		AXT,1	SXA,1
			ZEROVEC
			0
			WMATFLAG	# TURN OFF WMATRIX INTEGRATION.
			RSCALE
			SCALER		# SET SCALE OF POSITION.
			4
			SCALDELT	# ALSO DEVIATION.
			18D
			SCALEDT		# AND TIME STEP.
			EARTHTAB
			PBODY
			TESTTET
			STEPEXIT
		STORE	TDELTAV		# ZERO POSITION DEVIATION.

		NOLOD	0
		STORE	TNUV		# ALSO VELOCITY.

		NOLOD	0		# AND TIME SINCE RECTIFICATION, TIME, 
		STORE	TC		# AND KEPLER X.
		AXT,1	1
		AST,1	EXIT
			12D
			6

		CS	INITMSK
		MASK	STATE
		TS	STATE
		TC	INTPRET

RVTOMID		VXSC*	1		# TRANSFORM POSITION AND VELOCITY TO
		VXM	VSLT
			RRECT +12D,1
			SCLRAVMD +12D,1
			REFSMMAT
			2
		STORE	RRECT +12D,1

		NOLOD	0
		STORE	RCV +12D,1

		TIX,1	0
			RVTOMID

		DMP	1
		TSLT
			TAVEGON
			SCLTAVMD
			3
		STORE	TDEC


TESTTET		LXC,1	1
		AST,1	TIX,1
			MEASMODE
			1
			+3

		ITC	0
			+4

		TEST	0
			UPDATFLG
			NOSTATE

		STZ	0
			OVFIND

		DSU	1
		TSLT	DDV
			TDEC
			TET
			9D
			EARTHTAB +9D
		STORE	DT/2

		BOV	3
		ABS	DSU
		BMN	DAD
		DSU	BMN
			USEMAXDT
			DT/2
			DT/2MIN
			DODCSION
			DT/2MIN
			DT/2MAX
			TIMESTEP

USEMAXDT	DMOVE	1
		SIGN
			DT/2MAX
			DT/2
		STORE	DT/2

		ITC	0
			TIMESTEP


DODCSION	ITC	0		# RECTIFY TO OBTAIN FULL POSITION
			RECTIFY		# AND VELOCUTY VECTORS.

		SMOVE	1
		BMN	BZE
			MEASMODE	# TEST MEASMODE.
			AVEGON		# MEASMODE = -1.
			IGN-4SEC	# MEASMODE = 0.

		AXT,1	1		# MEASMODE = +1.
		AST,1
			12D
			6

RVTOAVE		VXSC*	1		# TRANSFORM POSITION AND VELOCITY VECTORS
		MXV	VSLT
			RRECT +12D,1
			SCLRMDAV +12D,1
			REFSMMAT
			1
		STORE	RIGNTION +12D,1

		VXSC*	1
		MXV	VSLT
			RAVEGON +12D,1
			SCLRMDAV +12D,1
			REFSMMAT
			1
		STORE	RAVEGON +12D,1

		TIX,1	0
			RVTOAVE

		ITCI	0
			MIDEXIT		# RETURN. 


AVEGON		VMOVE	0		# SAVE POSITION AND VELOCITY AT
			RRECT		# AVERAGE G ON TIME.
		STORE	RAVEGON

		VMOVE	0
			VRECT
		STORE	VAVEGON

		LXC,1	1
		AST,1	TIX,1
			MEASMODE
			1
			RVTOAVE -4

		DAD	1
		AXT,1	SXA,1
			TDEC
			1M555S		# 1 MINUTES, 55.4 SECS
			0
			MEASMODE	# MAKE MEASMODE 0.
		STORE	TDEC

		ITC	0
			TESTTET		# CONTINUE INTEGRATION.



IGN-4SEC	VXSC	1		# TRANSFORM AND SAVE POSITION ONLY
		MXV	VSLT
			RRECT
			SCLRMDAV
			REFSMMAT
			1
		STORE	RIG-4SEC

		DAD	1
		AXT,1	SXA,1
			TDEC
			17.75SEC	# ADD 17.75 SECONDS TO DECISION TIME.
			1
			MEASMODE	# MAKE MEASMODE +1.
		STORE	TDEC

		ITC	0
			TESTTET		# DO LAST INTEGRATION STEP.

1M555S		2DEC	11540
17.75SEC	2DEC	1775
INITMSK		OCT	30000
