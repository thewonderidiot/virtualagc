### FILE="Main.annotation"
## Copyright:	Public domain.
## Filename:	R30.agc
## Purpose:	A section of Skylark revision 048.
##		It is part of the source code for the Apollo Guidance Computer (AGC)
##		for Skylab-2, Skylab-3, Skylab-4, and ASTP. No original listings of
##		this software are available; instead, this file was created via
##		disassembly of dumps of the core rope modules actually flown on
##		Skylab-2. Access to these modules was provided by the New Mexico
##		Museum of Space History.
## Assembler:	yaYUL
## Contact:	Ron Burkey <info@sandroid.org>.
## Website:	www.ibiblio.org/apollo/index.html
## Mod history:	2023-09-04 MAS  Created from Artemis 072.
##		2024-03-05 MAS  Updated for Skylark 48.

# SUBROUTINE NAME:  V82CALL
# MOD NO:  0								DATE: 		16 FEB 67
# MOD BY:  RR BAIRNSFATHER						LOG SECTION:	R30
# MOD NO: 1		MOD BY: RR BAIRNSFATHER		DATE: 11 APR 67		SR30.1 CHANGED TO ALLOW MONITOR OPERN
# MOD  NO: 2		MOD  BY ALONSO			DATE : 11 DEC 67	VB82 PROGRAM REWRITTEN
# MOD NO: 3		MOD  BY ALONSO			DATE: 26 MAR 68		PROG MOD TO HANDLE DIF EARTH/MOON SCALE
# MOD NO- 4		MOD  BY CROCKER			DATE- 27 AUG 69		PROG MOD FOR TIME OPTION.
#  NEW FUNCTIONAL DESCRIPTION-	   CALLED BY VERB 82 ENTER.  PRIORITY 7
#    USED THROUGHOUT.		   CALCULATE AND DISPLAY ORBITAL PARAMETERS
#
# 1. IF AVERAGE G IS OFF:
#	  FLASH DISPLAY V04N12. R2 INDICATES WHICH SHIP'S STATE VECTOR IS
#	   TO BE UPDATED. INITIAL CHOICE IS THIS SHIP (R2=1). ASTRONAUT
#	   CAN CHANGE TO OTHER SHIP BY V22EXE, WHERE X NOT EQ 1.
#	   FLASH DISPLAY TIME V06N16. INITIAL CHOICE = 0 = PRESENT TIME.
#	   ASTRONAUT CAN INSERT A TIME BY V25E, R1=HR R2=MIN R3=CSEC.
#	   CHOSEN STATE VECTOR UPDATED TO CHOSEN TIME BY THISPREC/OTHPREC
#	  CALLS SR30.1 (WHICH CALLS TFFCONMU + TFFRP/RA) TO CALCULATE
#	   RPER (PERIGEE RADIUS), RAPO (APOGEE RADIUS), HPER (PERIGEE
#	   HEIGHT ABOVE LAUNCH PAD OR LUNAR LANDING SITE), HAPO (APOGEE
#	   HEIGHT AS ABOVE), TPER (TIME TO PERIGEE), TFF (TIME TO
#	   INTERSECT 300 KFT ABOVE PAD OR 35KFT ABOVE LANDING SITE).
#	  FLASH MONITOR V16N44 (HAPO, HPER, TFF).TFF IS -59M59S IF IT WAS
#	   NOT COMPUTABLE, OTHERWISE IT INCREMENTS ONCE PER SECOND.
#	   IF P00 IS RUNNING, CALL DELRSPL SPLASH ERROR COMPUTATION.
#	   ASTRONAUT HAS OPTION TO MONITOR TPER BY KEYING IN N 32 E.
#	   DISPLAY IS IN HMS, IS NEGATIVE (AS WAS TFF), AND INCREMENTS
#	   ONCE PER SECOND ONLY IF TFF DISPLAY WAS -59M59S.
#
# 2. IF AVERAGE G IS ON:
#	  CALLS SR30.1 APPROX EVERY TWO SECS.  STATE VECTOR IS ALWAYS
#	   FOR THIS VEHICLE. V82 DOES NOT DISTURB STATE VECTOR.  RESULTS
#	   OF SR30.1 ARE RAPO, RPER, HAPO, HPER, TPER, TFF.
#	  FLASH MONITOR V16N44 (HAPO, HPER, TFF).
#	   IF MODE IS P11, THEN CALL DELRSPL SO ASTRONAUT CAN MONITOR
#	   RESULTS BY N50E. SPLASH COMPUTATION DONE ONCE PER TWO SECS.
#
#
# ADDENDUM: HAPO AND HPER SHOULD BE CHANGED TO READ HAPOX AND HPERX IN THE
#	    ABOVE REMARKS.
#
# CALLING SEQUENCE:  VERB 82 ENTER.
#
#
# SUBROUTINES CALLED:	SR30.1, GOXDSPF
#			MAYBE - THISPREC, OTHPREC, LOADTIME, DELRSPL,
#				VEHOPT, TIMEOPT
# NORMAL EXIT MODES:  TC ENDEXT
#
# ALARMS:  NONE
#
# OUTPUT:  HAPOX    (-29) M
#	   HPERX    (-29) M
#	   RAPO     (-29) M EARTH
#		    (-27) M MOON
#	   RPER     (-29) M EARTH
#		    (-27) M MOON
#	   TFF      (-28) CS	  CONTAINS NEGATIVE QUANTITY
#	   -TPER    (-28) CS	  CONTAINS NEGATIVE QUANTITY
#	   RSP-RREC (-29) M	  IF DELRSPL CALLED
#
# ERASABLE INITIALIZATION REQUIRED: STATE VECTOR.
#
# DEBRIS:  QPRET, RONE, VONE, TFF/RTMU, HPERMIN, RPADTEM.
#	   MAYBE:  TSTART82, V82FLAGS, TDEC1.


		EBANK=	HAPOX
		SBANK=	PINSUPER
		SETLOC	R30LOC
		BANK
		COUNT*	$$/R30

V82CALL		TC	INTPRET
		BON	CALL
			AVEGFLAG
			V82GON		# IF AVERAGE G ON
			VEHOPT		# IF OFF, ALLOW VEHICLE OPTION
		EXIT
		CAF	BIT4		# 80 MS
		TC	WAITLIST
		EBANK=	TFF
		2CADR	TICKTEST
		RELINT
		SBANK=	LOWSUPER
V82GOFLP	CAF	TFFBANK		# MAJOR RECYCLE LOOP ENTRY
		TS	EBANK
		CAF	ZERO
		TS	V82FLAGS	# ZERO FLAGS FOR TICKTEST. INHIBITS
					# DECREMENTING OF TFF AND -TPER.
		CAF	PRIO7
		TC	FINDVAC		# V82GOFF1 WILL EXECUTE STATE VECTOR
		EBANK=	TFF		# UPDATE AND ORBIT CALCULATIONS FOR
		2CADR	V82GOFF1	# SELECTED VEHICLE ABOUT PROPER BODY.
		RELINT
V82STALL	CAF	THREE		# STALL IN THIS LOOP AND WITHOLD V 16 N 44
		MASK	V82FLAGS	# UNTIL STATE VECTOR UPDATE SETS ONE OF
		CCS	A		# OUR FLAG BITS.
		TC	FLAGGON		# EXIT FROM STALL LOOP.
		TC	BANKCALL
		CADR	1SECDELY
		TC	V82STALL

FLAGGON		CAF	V16N44		# MONITOR HAPO,HPER,TFF.
		TC	BANKCALL
		CADR	GOXDSPF
		TC	B5OFF		# TERM THIS TELLS TICKTEST TO KILL ITSELF
		TC	B5OFF		# PROCEED  DITTO
		TC	V82GOFLP	# RECYCLE  RECOMPUTE STATE VECT + DISPLAY

V16N44		VN	1644
TFFBANK		ECADR	TFF


V82GOFF1	TC	INTPRET
		DLOAD	CALL
			ZEROVECS	# INITIAL TIME = 0 = PRES. TIME
			TIMEOPT		# ALLOW TIME SELECTION
		STODL	TSTART82	# TIME FOR INTERNAL USE (=TDEC1 TIME)
			OPTIONEV
		SR1	BHIZ	
			THISSHIP	
		CALL			# CALL STATE VECTOR UPDATE FOR OTHER SHIP.
			OTHPREC
BOTHSHIP	VLOAD			# MOVE RESULTS INTO TFFCONIC STORAGE AREAS
			RATT		#  TO BE CALLED BY SR30.1.
		STOVL	RONE		# RATT AT (-29)M FOR EARTH OR MOON
			VATT
		STCALL	VONE
			EARTHPAD
THISSHIP	CALL			# CALL STATE VECTOR UPDATE FOR THIS SHIP.
			THISPREC
		GOTO
			BOTHSHIP
			
TIMEOPT		STORE	DSPTEMX
 +1		STQ	EXIT
			VEHRET
		CAF	V06N16X
		TC	BANKCALL
		CADR	GOXDSPF 
		TC	ENDEXT
		TC	+2
		TC	-5
		TC	INTPRET
		DLOAD	BZE
			DSPTEMX
			GETNOW
STRTDEC1  	STCALL	TDEC1 
			VEHRET
GETNOW		RTB	GOTO
			LOADTIME
			STRTDEC1
V06N16X		VN	0616
# THE FOLLOWING CONSTANTS ARE PAIRWISE INDEXED. DO NOT SEPARATE PAIRS.

1/RTMUE		2DEC*	.50087529 E-5 B17*

MINPERE		2DEC	91440 B-29	# 300 KFT (-29)M FOR EARTH

EARTHPAD	CALL
			SR30.1		# CALCULATE ORBITAL PARAMETERS
		SLOAD	BHIZ	
			MODREG		# ARE WE IN POO
			CANDEL		# YES, DO DELRSPL
SPLRET1		RTB	DSU
			LOADTIME
			TSTART82	# PRESENT TIME - TIME V82GOFF1 BEGAN
		STODL	TSTART82	#                SAVE IT.
			-TPER		# SR30.1 SETS -TPER=0 IF HPER L/
		BZE			#  HPERMIN (300 OR 35) KFT.
			TICKTFF		# (-TPER = 0)
TICKTPER	DLOAD	DAD		# (-TPER NON ZERO)  TFF WAS NOT COMPUTED,
			-TPER		# BUT WAS SET TO 59M59S.DONT TICK TFF, DO
			TSTART82	# TICK -TPER. DISPLAY BOTH.
		STORE	-TPER           # -TPER CORRECTED FOR TIME SINCE V82GOFF1
		EXIT                    # BEGAN.
		CAF	BIT1		# INFORMS TICKTEST TO INCREMENT ONLY -TPER
		TC	TICKTFF	+6	
TICKTFF		DLOAD	DAD		# (-TPER=0)  TFF WAS COMPUTED.TICK TFF.
			TFF		# DO NOT TICK -TPER.DISPLAY TFF, BUT NOT
			TSTART82	# -TPER.
		STORE	TFF		# TFF CORRECTED FOR TIME SINCE V82GOFF1
		EXIT			# BEGAN.
		CAF	BIT2
 +6		TS	V82FLAGS	# INFORMS TICKTEST TO INCREMENT ONLY TFF.
		TC	ENDOFJOB
TICKTEST	CAF	BIT5		# THIS WAITLIST PROGRAM PERPETUATES ITSELF
		MASK	EXTVBACT	# ONCE A SEC UNTIL BIT 5 OF EXTVBACT =0.
		CCS	A
		TC	DOTICK
		CAF	PRIO25
		TC	NOVAC		# TERMINATE V 82.CANT CALL ENDEXT IN RUPT.
		EBANK=	EXTVBACT
		2CADR	ENDEXT

		TC	TASKOVER
DOTICK		CAF	1SEC		# RE-REQUEST TICKTEST.
		TC	WAITLIST
		EBANK=	TFF
		2CADR	TICKTEST

		CAF	THREE
		MASK	V82FLAGS
		INDEX	A
		TC	+1
		TC	TASKOVER	# IF NO FLAGBITS SET DONT CHANGE TFF OR
					# -TPER, BUT CONTINUE LOOP.
		TC	TPERTICK	# ONLY BIT 1 SET. INCR -TPER BY 1 SEC.
TFFTICK		CAF	1SEC		# ONLY BIT 2 SET. INCR TFF BY 1 SEC.
		TS	L
		CAF	ZERO
		DAS	TFF
		TC	TASKOVER
TPERTICK	CAF	1SEC
		TS	L
		CAF	ZERO
		DAS	-TPER
		TC	TASKOVER
V82GON		EXIT			#  AVERAGE G ON. USE CURRENT STATE VECTOR
					# FOR ORBITAL PARAMETER CALCULATIONS.
		CAF	PRIO7		# LESS THAN LAMBERT
		TC	FINDVAC		# V82GON1 WILL PERFORM ORBIT CALCULATIONS
		EBANK=	TFF		# ABOUT PROPER BODY APPROX ONCE PER SEC.
		2CADR	V82GON1

		RELINT
		CCS	NEWJOB		# WITHOLD V16 N44 UNTIL FIRST ORBIT CALC
		TC	CHANG1		# IS DONE. NOTE:V82GON1 (PRIO7, FINDVAC
					# JOB) IS COMPLETED BEFORE V82GON (PRIO7,
					# NOVAC JOB).
V82REDSP	CAF	V16N44		# MONITOR HAPO, HPER, TFF
		TC	BANKCALL
		CADR	GOXDSPF
		TC	B5OFF		# TERM THIS TELLS V82GON1 TO KILL ITSELF.
		TC	B5OFF		# PROC DITTO.
		TC	V82REDSP	# RECYCLE

V82GON1		TC	INTPRET		# THIS EXEC PROGRAM PERPETUATES ITSELF
					# ONCE A SEC UNTIL BIT 5 OF EXTVBACT =0.
		VLOAD	GOTO		# HOLDS OFF CCS NEWJOB BETWEEN RN AND
			RN		#   VN FETCH SO RN , VN ARE FROM SAME
			NEXTLINE	#   STATE VECTOR UPDATE.
NEXTLINE	STOVL	RONE		#  RN AT (-29)M FOR EARTH OR MOON
			VN
		STORE	VONE		# VN AT (-7)M/CS FOR EARTH OR MOON

V82GON2		CALL
			SR30.1
		EXIT
		TC	CHECKMM
		DEC	11
		TC	V82GON3		# NOT IN MODE 11.
		TC	INTPRET		# IN MODE 11 OR P00
CANDEL		CALL
			INTSTALL	# DELRSPL DOES INTWAKE
		DLOAD	CALL
			TFF
			DELRSPL		# RETURN IS TO NEXT LINE ( SPLRET ).
SPLRET		SLOAD	BHIZ
			MODREG
			SPLRET1
		EXIT
V82GON3		CAF	BIT5
		MASK	EXTVBACT	# SEE IF ASTRONAUT HAS SIGNALLED TERMINATE
		EXTEND
		BZF	ENDEXT		# YES, TERMINATE VB 82 LOOP
		TC	BANKCALL	# WAIT ONE SECOND BEFORE REPEATING
		CADR	1SECDELY	#    ORBITAL PARAMETER COMPUTATION.
		TC	V82GON1
		
# SUBROUTINE NAME:	SR30.1
# MOD NO:  0									DATE: 		16 FEB 67
# MOD BY:  RR BAIRNSFATHER							LOG SECTION: 	R32
# MOD NO:  1		MOD BY:  RR BAIRNSFATHER	DATE: 11 APR 67		SR30.1 CHANGED TO ALLOW MONITOR OPERN
# MOD NO:  2		MOD BY:  RR BAIRNSFATHER	DATE:  14 APR 67	ADD OVFL CK FOR RAPO
# MOD NO:  3		MOD BY ALONSO			DATE: 11 DEC 67		SUBROUTINE REWRITTEN
# MOD NO: 4		MOD BY ALONSO			DATE: 26 MAR 68		PROG MOD TO HANDLE DIF EARTH/MOON SCALE
# MOD NO:  5		MOD BY:  RR BAIRNSFATHER	DATE:  6 AUG 68		OVFL CK FOR HAPO & HPER.VOIDS MOD #2.
#
# NEW FUNCTIONAL DESCRIPTION:	ORBITAL PARAMETERS DISPLAY FOR NOUNS 32  AND 44.
# SR30.1 CALLS TFFCONMU AND TFFRP/RA TO CALCULATE RPER (PERIGEE RADIUS),
# RAPO (APOGEE RADIUS), HPER (PERIGEE HEIGHT ABOVE LAUNCH PAD OR LUNAR
# LANDING SITE), HAPO (APOGEE HEIGHT AS ABOVE), TPER (TIME TO PERIGEE),
# TFF (TIME TO INTERSECT 300 KFT ABOVE PAD OR 35KFT ABOVE LANDING SITE).
# IF HPER IS GREATER THAN OR EQUAL TO HPERMIN, CALCULATES TPER AND STORES
# NEGATIVE    IN -TPER. OTHERWISE STORES +0 IN -TPER. WHENEVER TPER IS
# CALCULATED, TFF IS NOT COMPUTABLE AND DEFAULTS TO -59MIN 59SEC. IF HAPO
# WOULD EXCEED 9999.9 NM, IT IS LIMITED TO THAT VALUE FOR DISPLAY.
#
# ADDENDUM: HAPO AND HPER SHOULD BE CHANGED TO READ HAPOX AND HPERX IN THE
#	    ABOVE REMARKS.
#
# CALLING SEQUENCE:	CALL
#				SR30.1
# SUBROUTINES CALLED:	TFFCONMU, TFFRP/RA, CALCTPER, CALCTFF
# NORMAL EXIT MODE:	CALLING LINE +1 (STILL IN INTERPRETIVE MODE)
# ALARMS:		NONE
# OUTPUT:  RAPO   (-29) M EARTH   APOGEE RADIUS   EARTH CENTERED COORD.
#		  (-27) M MOON			  MOON CENTERED COORD.
#	   RPER   (-29) M EARTH   PERIGEE RADIUS  EARTH CENTERED COORD.
#		  (-27) M MOON			  MOON CENTERED COORD.
#	   HAPOX  (-29) M	  APOGEE ALTITUDE ABOVE PAD OR LAND. SITE MAX VALUE LIMITED TO 9999.9 NM.
#	   HPERX  (-29) M	  PERIGEE ALT. ABOVE PAD OR LAND. SITE    MAX VALUE LIMITED TO 9999.9 NM.
#	   TFF    (-28) CS	  TIME TO 300KFT OR 35KFT ALTITUDE
#	   -TPER  (-28) CS	  TIME TO PERIGEE
# ERASABLE INITIALIZATION REQUIRED -
#	   TFF/RTMU (+17) EARTH   RECIPROCAL OF PROPER GRAV CONSTANT FOR
#		    (+14) MOON    EARTH OR MOON = 1/SQRT(MU).
#	   RONE	   (-29) M	  STATE VECTOR
#	   VONE	   (-7)  M/CS	  STATE VECTOR
#	   RPADTEM (-29) M EARTH  RADIUS OF LAUNCH PAD OR LUNAR LANDING
#		   (-27) M MOON	  SITE.
#	   HPERMIN (-29) M EARTH  (300 OR 35)KFT MINIMUM PERIGEE ALTITUDE
#		   (-27) M MOON	  ABOVE LAUNCH PAD OR LUNAR LANDING SITE.
#
# DEBRIS:  QPRET, PDL, S2

		COUNT*	$$/SR30S

SR30.1		SETPD	STQ		# INITIALIZE PUSHDOWN LIST.
			0
			S2
					# SR30.1 INPUT:	RONE AT (-29)M EARTH/MOON
					#		VONE AT (-7)M/CS
					# TFFCONMU,TFFRP/RA,CALCTPER AND CALCTFF
					# CALLS REQUIRE :
					# EARTH CENTERED (NO RESCALING REQUIRED)
					#		RONE SCALED TO B-29 M
					#		VONE SCALED TO B-7 M/CS
TFFCALLS	DLOAD	CALL
			1/RTMUE
			TFFCONIC
		CALL			# TFFRP/RA COMPUTES RAPO,RPER.
			TFFRP/RA
					# RETURNS WITH RAPO IN D(MPAC).
		DSU	CALL
			RPAD		# IF HAPO > MAXNM, SET HAPO =9999.9 NM.
			MAXCHK		# OTHERWISE STORE (RAPO-RPAD) IN HAPO.
STORHAPO	STODL	HAPOX
			RPER
		DSU
			RPAD		# GIVES HPER AT (-29)M.
		STORE	MPAC +4		# SAVE THIS FOR COMPARISON TO MINPERE.
		CALL			# IF HPER > MAXNM, SET HPER = 9999.9 NM.
			MAXCHK
STORHPER	STODL	HPERX		# STORE (RPER - RPAD) INTO HPERX.
			MPAC +4
		DSU	BPL		# MINPERE AT (-29)M
			MINPERE		# IF HPER L/   MINPERE (300)KFT,
			DOTPER		#  THEN ZERO INTO -TPER.
		DLOAD	GOTO		#   OTHERWISE CALCULATE TPER.
			HI6ZEROS
			SKIPTPER
DOTPER		DLOAD	CALL
			RPER
			CALCTPER
		DCOMP			# TPER IS PUT NEG INTO -TPER.
SKIPTPER	STODL	-TPER
			MINPERE		# MINPERE AT (-29)M FOR EARTH
		DAD	CALL
			RPAD		# RPAD AT (-29)M FOR EARTH
			CALCTFF		# GIVES 59M59S FOR TFF IF RPER G/
		DCOMP			#   MINPERE + RPAD. (TPER WAS NON ZERO)
		STCALL	TFF		# OTHERWISE COMPUTES TFF.	(GOTO)
			S2

MAXCHK		DSU	BPL		# IF C(MPAC) > 9999.9 NM, MPAC = 9999.9 NM
			MAXNM
			+3		# OTHERWISE C(MPAC) = B(MPAC).
		DAD	RVQ
			MAXNM
 +3		DLOAD	RVQ		# (USED BY P30 - P37 ALSO)
			MAXNM

MAXNM		2OCT	0106505603

## This function was located in the P30-P31 log section in Artemis. It has been moved here,
## where it more logically belongs, to match Skylark memory ordering.

# SUBROUTINE NAME:	DELRSPL		(CONTINUATION OF V 82 IN CSM IF P11 ACTI
# TRANSFERRED COMPLETELY FROM SUNDISK, P30S REV 33. 9 SEPT 67.
# MOD NO: 0	MOD BY: ZELDIN		DATE:
# MOD NO: 1	MOD BY: RR BAIRNSFATHER	DATE: 11 APR 67
# MOD NO: 2	MOD BY: RR BAIRNSFATHER	DATE: 12 MAY 67		ADD UR.RT CALC WHEN BELOW 300K FT
# MOD NO: 2.1	MOD BY: RR BAIRNSFATHER	DATE: 5 JULY 67		FIX ERROR IN MOD. 2.
# MOD NO: 3	MOD BY: RR BAIRNSFATHER	DATE: 12 JUL 67		CHANGE SIGN OF DISPLAYED ERROR.
# MOD 4		MOD BY  S.ZELDIN	DATE  3 APRIL 68	CHANGE EQUATIONS FOR L/D=.18 WHICH REPLA
#
# FUNCTION:	CALCULATE (FOR DISPLAY ON CALL) AN APPROXIMATE MEASURE OF IN-PLANE SPLASH DOWN
#		ERROR. IF THE FREE-FALL TRANSFER ANGLE TO 300K FT ABOVE PAD RADIUS IS POSITIVE:
#		SPLASH ERROR= -RANGE TO TARGET + FREE-FALL TRANSFER ANGLE + ESTIMATED ENTRY ANGLE.
#		THE TARGET LOCATION AT ESTIMATED TIME OF IMPACT IS USED.  IF THE FREE-FALL TRANSFER
#		ANGLE IS NEGATIVE:  SPLASH ERROR= -RANGE TO TARGET
#		THE PRESENT TARGET LOCATION IS USED.
#
# CALLING SEQUENCE  CALLED AFTER SR30.1 IF IN CSM AND IF P11 OPERATING (UNDER CONTROL OF V82)
#
# SUBROUTINES CALLED:  VGAMCALC, TFF/TRIG, LALOTORV.
#
# EXIT 		RETURN DIRECTLY TO V 82 PROG. AT SPLRET
#
# ERASABLE INITIALIZATION   LEFT BY SR30.1 AND V82GON1
#
# OUTPUT:	RSP-RREC  RANGE IN REVOLUTIONS   		DSKY DISPLAY IN N. MI.
#
# DEBRIS:	QPRET, PDL0 ... PDL7, PDL10
# 		THETA(1)

		SETLOC	DELRSPL1
		BANK
		COUNT*	$$/P30		# PROGRAMS: P30 EXTERNAL DELTA V
		
DELRSPL		STORE	8D
		BPL	DSU
			CANTDO		# GONE PAST 300K FT ALT
			1BITDP
		BOV	CALL
			CANTDO		# POSMAX INDICATES NO 300K FT SOLUTION.
			VGAMCALC	# +GAMMA(REV) IN PMAC,V300 MAG(B-7)=PDL 0
		PUSH	CALL
			TFF/TRIG
		CALL
			AUGEKUGL
		PDDL	ACOS		# T ENTRY PDL 6
			CDELF/2
		DAD
			4
GETARG		STOVL	THETA(1)
			LAT(SPL)
		STODL	LAT
			HI6ZEROS
		STODL	ALT		# ALT=0 = LAT +4
			PIPTIME
		BON	DLOAD
			V37FLAG
			+2
			TSTART82
		DSU	DAD
			8D
		CLEAR	CALL
			ERADFLAG
			LALOTORV	# R RECOV. IN ALPHAV AND MPAC

		UNIT	PDVL
			RONE
		UNIT	DOT
		SL1	ARCCOS
		BDSU			# ERROR = THETA EST - THETA TARG
					# NEGATIVE NUMBER SIGNIFIES THAT WILL FALL SHORT.
					# POSITIVE NUMBER SIGNIFIES THAT WILL OVERSHOOT.
			THETA(1)
DELRDONE	STCALL	RSP-RREC	# DOWNRANGE RECOVERY RANGE ERROR	/360
			INTWAKE0
		CALL
			SPLRET
CANTDO		DLOAD	PDDL		# INITIALIZE ERASE TO DOT TARGET AND UR
					# FOR RANGE ANGLE.
			HIDPHALF	# TO PDL 0 FOR DEN IN DDV.
			HI6ZEROS
		PUSH			# ZERO TO PDL 2 FOR PHI ENTRY
		STCALL	8D
			GETARG		# GO SET RSP-RREC =0
			
AUGEKUGL	VLOAD
			X1CON -2
		STODL	X1 -2
			0
		DSU	BMN
			V(21K)
			LOOPSET
		XSU,1	XCHX,2
			S1
			X1
		XCHX,2	DSU
			S1
			V(3K)
		BMN	XCHX,2
			LOOPSET
			S1
		DSU	BMN
			V(4K)
			LOOPSET
		XCHX,2	XCHX,2
			S1
			X1
		DSU	BMN
			V(400)
			LOOPSET
		SXA,1
			S1
LOOPSET		INCR,1	GOTO
		DEC	1
			K1K2LOOP
K2CALC		SXA,1
			S1
K1K2LOOP	DLOAD	DSU*
			0
			V(32K) +1,1
		DMP*	DAD*
			YK1K2 +1,1
			CK1K2 +1,1
		PDDL	TIX,1
			2
			K2CALC
		DSU	BDDV
		PUSH	BOV		# PHI ENTRY PDL 4D
			MAXPHI
		BMN	DSU
			MAXPHI
			MAXPHIC
		BPL
			MAXPHI
PHICALC		DLOAD	DSU
			0
			V(26K)
		BPL	DLOAD
			TGR26
			TLESS26
		DDV
			0
TENT		DMP	RVQ
			4D
TGR26		DLOAD	GOTO
			TGR26CON
			TENT
			
MAXPHI		DLOAD	PDDL
			MAXPHIC
		GOTO
			PHICALC
MAXPHIC		2DEC	.09259298	# 2000 NM FOR MAXIMUM PHI ENTRY

		COUNT*	$$/P30

		
					# 		BELOW
					# <<<< TABLE IS INDEXED. KEEP IN ORDER >>>

		2DEC	7.07304526 E-4		# 5500
		
		2DEC	3.08641975 E-4		# 2400
		
		2DEC	3.08641975 E-4		# 2400
		
		2DEC	-8.8888888 E-3		# -3.2
		
		2DEC	2.7777777 E-3		# 1
		
CK1K2		2DEC	6.6666666 E-3		# 2.4

		2DEC	0			# 0
		
		2DEC*	-1.86909989 E-5 B7* 	# -.443
		
		2DEC	0
		
		2DEC*	1.11639691 E-3 B7*	# .001225
		
		2DEC*	9.56911636 E-4 B7*	# .00105
		
YK1K2		2DEC*	2.59733157 E-4 B7*	# .000285

V(400)		2DEC	1.2192 B-7

V(28K)		2DEC	85.344 B-7

V(3K)		2DEC	9.144 B-7

V(24K)		2DEC	73.152 B-7

		2DEC	85.344 B-7
		
V(32K)		2DEC	97.536 B-7

V(4K)		2DEC	12.192 B-7

V(21K)		2DEC	64.008 B-7

TLESS26		2DEC*	5.70146688 E7 B-35*	# 8660PHI/V

TGR26CON	2DEC	7.2 E5 B-28		# PHI/3

V(26K)		2DEC	79.248 B-7		# 26000

X1CON		DEC	10


		DEC	8
		DEC	6
					# <<<< TABLE IS INDEXED. KEEP IN ORDER >>>
					#		ABOVE
